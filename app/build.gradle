plugins {
    id 'com.android.application'
    id 'com.google.protobuf'
}

// Remember to config your keystore settings in local.properties or in the below lines
def keyfile = file('./keystore.jks')
def keystorePSW = 'CHANGEME'
def keystoreAlias = 'CHANGEME'
def keystoreAliasPSW = 'CHANGEME'

Properties properties = new Properties()
properties.load(project.rootProject.file('gradle.properties').newDataInputStream())
def keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keyfile = file(keystoreFilepath)
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
}

android {
    signingConfigs {
        release {
            storeFile keyfile
            storePassword keystorePSW
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdk 33

    defaultConfig {
        applicationId "com.jacopomii.googledialermod"
        minSdk 21
        targetSdk 33
        versionCode 106
        versionName "1.06"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'com.jacopomii.googledialermod'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation "com.github.topjohnwu.libsu:core:5.0.3"
    implementation 'com.google.protobuf:protobuf-javalite:3.8.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}